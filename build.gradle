plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'org.jmailen.kotlinter' version '1.16.0'
}

allprojects {
    group = 'com.softwareberg.rinca'
    version = '0.9.1'
}

defaultTasks 'build'

subprojects {
    apply plugin: 'maven'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.8.0'
    }

    jacocoTestReport {
        reports {
            csv.enabled false
            html.enabled true
            xml.enabled true
        }
        dependsOn test
    }

    task ktLint(type: org.jmailen.gradle.kotlinter.tasks.LintTask, group: 'verification', description: 'Kotlin lint') {
        source = file('src')
        continuationIndentSize = 4
        reports = [:]
    }

    kotlinter {
        continuationIndentSize = 4
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    test {
        useJUnitPlatform()
    }

    check {
        dependsOn = check.dependsOn + [ktLint]
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        compile 'org.jetbrains.kotlin:kotlin-reflect'

        testCompile 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
        testCompile 'com.nhaarman:mockito-kotlin:1.6.0'
        testCompile 'org.assertj:assertj-core:3.11.0'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
