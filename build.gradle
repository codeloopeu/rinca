allprojects {
    group = 'com.softwareberg'
    version = '0.4.0'
}

buildscript {
    ext.kotlin_version = '1.1.0-rc-91'
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

defaultTasks 'build'

subprojects {
    apply plugin: 'maven'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    }

    jacocoTestReport {
        reports {
            csv.enabled false
            html.enabled true
            xml.enabled true
        }
        dependsOn test
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile 'junit:junit:4.+'
        testCompile 'com.nhaarman:mockito-kotlin:1.+'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
