allprojects {
    group = 'com.softwareberg'
    version = '0.6.0'
}

buildscript {
    ext.kotlin_version = '1.1.0'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

defaultTasks 'build'

subprojects {
    apply plugin: 'maven'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    test {
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    jacocoTestReport {
        reports {
            csv.enabled false
            html.enabled true
            xml.enabled true
        }
        dependsOn test
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile 'junit:junit:4.+'
        testCompile 'com.nhaarman:mockito-kotlin:1.3.+'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}
